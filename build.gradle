plugins {
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0+"
    id "net.neoforged.gradle" version "[6.0.18,6.2)"
}

group = 'com.oitsjustjose.natprog'
version = "${mod_version}"
base { archivesName = "NaturalProgression-${game_version}-${mod_version}" }

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
minecraft {
    mappings channel: 'official', version: "${game_version}"
    copyIdeResources = true
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods { natprog { source sourceSets.main } }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods { natprog { source sourceSets.main } }
        }
    }
}


dependencies {
    minecraft "net.neoforged:forge:${game_version}-${neo_version}"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Natural-Progression",
                "Specification-Vendor"    : "oitsjustjose.com",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${mod_version}",
                "Implementation-Vendor"   : "oitsjustjose.com",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.register('publish') {
    dependsOn 'curseforge'
    dependsOn 'modrinth'
}

curseforge {
    apiKey = project.hasProperty("CURSEFORGE_API_KEY") ? project.CURSEFORGE_API_KEY : ""
    project {
        id = '349530'
        changelogType = 'markdown'
        changelog = file("./changelog.md")
        releaseType = 'release'
        addGameVersion "${game_version}"
        mainArtifact(jar) {
            displayName = "NatProg ${game_version} ${mod_version}"
        }
    }
}

modrinth {
    token = project.hasProperty("MODRINTH_API_KEY") ? project.MODRINTH_API_KEY : ""
    projectId = "VkVpYDxO"
    versionName = "NatProg ${game_version} ${mod_version}"
    versionNumber = "${game_version}-${mod_version}"
    versionType = "release"
    uploadFile = jar
    gameVersions = ["${game_version}"]
    loaders = ["forge"]
    changelog = file("./changelog.md").text
}
